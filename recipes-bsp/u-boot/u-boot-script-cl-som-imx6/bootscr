setenv loadaddr 0x10800000
setenv fdt_high 0xffffffff
setenv fdt_addr 0x15000000
setenv fdt_file imx6qp-sbc-imx6-hdmi.dtb
setenv bootm_low 0x15000000
setenv kernel_file zImage
setenv video vmalloc=256M cma=384M dmfc=3
setenv console ttymxc3,115200

test ! -z "$cpu_type" && test $cpu_type = 'MX6QP' && setenv fdt_file imx6qp-sbc-imx6-hdmi.dtb
test ! -z "$cpu_type" && test $cpu_type = 'MX6Q' && setenv fdt_file imx6q-sbc-imx6-hdmi.dtb

setenv make_root 'setenv rootdev /dev/${blockdevice}'
setenv make_args 'setenv bootargs console=${console} root=${rootdev} rootfstype=ext4 rw rootwait ${video}'

setenv setup_boot 'setenv bootlinux \"\
	if load ${bootdev} ${bootdev_id} ${loadaddr} ${kernel_file}; \
	then; \
		if load ${bootdev} ${bootdev_id} ${fdtaddr} ${fdt_file}; \
		then; \
			run make_root; \
			run make_args; \
			echo "Booting " ${bootdev}"["${bootdev_id}"]=>"${kernel_file}":"${fdt_file}; \
			bootz ${loadaddr} - ${fdtaddr}; \
		fi; \
	fi;\"'

setenv bootdev "sata"
setenv bootdev_id "0"
setenv blockdevice sda2

if sata init && sata dev ${bootdev_id}
then
	run setup_boot
	run bootlinux
fi

setenv bootdev "usb"
setenv bootdev_id "0"
setenv blockdevice sda2

if usb start && usb dev ${bootdev_id}
then
	run setup_boot
	run bootlinux
fi

setenv bootdev "mmc"
setenv bootdev_id "2"
setenv blockdevice mmcblk2p2
if mmc dev ${bootdev_id} && mmc rescan
then
	run setup_boot
	run bootlinux
fi

setenv bootdev "mmc"
setenv bootdev_id "3"
setenv blockdevice mmcblk3p2
if mmc dev ${bootdev_id} && mmc rescan
then
	run setup_boot
	run bootlinux
fi
